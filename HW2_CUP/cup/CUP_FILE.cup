/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;

parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getCharPos());
		System.out.print("] ");
		System.out.print(message + " " + info);
		System.out.println("My message:" + message);
		throw new IllegalStateException("ERROR("+lexer.getLine()+")\n");
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
    s = lexer.next_token();
    if (s.sym == TokenNames.ERROR) {
        throw new IllegalArgumentException("ERROR\n");
    }
	//System.out.print(s);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getCharPos());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal TYPE_STRING;
terminal TYPE_INT;
terminal TYPE_VOID;
terminal NEW;
terminal LT;
terminal GT;
terminal RETURN;
terminal NIL;
terminal ARRAY;
terminal COMMA;
terminal CLASS;
terminal EXTENDS;
terminal ERROR;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_EXP exp;
non terminal AST_VAR var;
non terminal AST_STMT stmt;
non terminal AST_STMT_LIST stmtList;
non terminal AST_PROGRAM Program;
non terminal AST_DEC dec;
non terminal AST_VARDEC varDec;
non terminal AST_NEWEXP newExp;
non terminal AST_BINOP BINOP;
non terminal AST_EXP_LIST expList;
non terminal AST_CFIELD cField;
non terminal AST_DEC_LIST decList;
non terminal AST_FUNC_DEC funcDec;
non terminal AST_ARRAY_TYPE_DEF arrayTypedef;
non terminal AST_TYPE type;
non terminal AST_TYPEID_LIST TypeIDList;
non terminal AST_CLASSDEC classDec;
non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_EXP_PAREN expParen;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence nonassoc ASSIGN;
precedence left EQ;
precedence left LT;
precedence left GT;
precedence left PLUS;
precedence left MINUS;
precedence left TIMES;
precedence left DIVIDE;
precedence nonassoc LBRACK;
precedence nonassoc LPAREN;
precedence nonassoc RPAREN;
precedence left DOT;
/************************/
/* START WITH: stmtList */
/************************/
start with Program;

/********************/
/* DERIVATION RULES */
/********************/


stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AST_STMT_LIST(s,l);    :}
					| stmt:s				{: RESULT = new AST_STMT_LIST(s,null); :}
					;


expList	    ::=		exp:e COMMA	expList:l		{: RESULT = new AST_EXP_LIST(e,l);    :}
					| exp:e				        {: RESULT = new AST_EXP_LIST(e,null); :}
					;
					

expParen	    ::=		exp:e                    	{: RESULT = new AST_EXP_PAREN(e);    :}
					;

/* ***** CHECK AGAIN ***** */
TypeIDList	::=		    type:t 	ID:name COMMA TypeIDList:l		{: RESULT = new AST_TYPEID_LIST(t,name,l);:}
					    | type:t ID:name				        {: RESULT = new AST_TYPEID_LIST(t,name,null); :}
					    ;


decList	::=		        dec:d 	decList:dl		    {: RESULT = new AST_DEC_LIST(d, dl);:}
					    | dec:d				        {: RESULT = new AST_DEC_LIST(d, null); :}
					    ;

cFieldList	       ::=	cField:cf cFieldList:cfl  {: RESULT = new AST_CFIELD_LIST(cf,cfl);    :}
                        | cField:cf				{: RESULT = new AST_CFIELD_LIST(cf,null); :}
                        ;


Program     ::=     decList:dl                              {: RESULT = new AST_PROGRAM_DEC(dl);:}
                    ;

dec         ::=     varDec:vd                           {: RESULT = new AST_DEC_VARDEC(vd);:}
                    | funcDec:fd                        {: RESULT = new AST_DEC_FUNC_DEC(fd);:}
                    | classDec:cd                       {: RESULT = new AST_DEC_CLASSDEC(cd);:}
                    | arrayTypedef:atd                  {: RESULT = new AST_DEC_ARRAY_TYPE_DEF(atd);:}
                    ;

type        ::=     TYPE_INT        {: RESULT = new AST_NEWTYPE(0, null);       :}
                    | TYPE_STRING   {: RESULT = new AST_NEWTYPE(1, null);       :}
                    | TYPE_VOID     {: RESULT = new AST_NEWTYPE(2, null);       :}
                    | ID:name       {: RESULT = new AST_NEWTYPE(3, name);       :}
                    ;

arrayTypedef  ::=    ARRAY ID:arrayName EQ type:t LBRACK RBRACK SEMICOLON      {: RESULT = new AST_ARRAY_TYPE_DEF_ID(arrayName,t);:}
                    ;


varDec        ::=     type:t ID:varName ASSIGN exp:e SEMICOLON     {: RESULT = new AST_VARDEC_TYPEEXP(t,varName,e);:}
                      | type:t ID:varName SEMICOLON                {: RESULT = new AST_VARDEC_TYPEEXP(t,varName, null);:}
                      | type:t ID:varName ASSIGN newExp:ne SEMICOLON      {: RESULT = new AST_VARDEC_TYPEEXP(t,varName,ne);:}
                      ;

funcDec        ::=     type:t ID:funcName LPAREN TypeIDList:til RPAREN LBRACE stmtList:sl RBRACE       {: RESULT = new AST_FUNC_DEC_STMT(t,funcName,til,sl);:}
                      | type:t ID:funcName LPAREN RPAREN LBRACE stmtList:sl RBRACE               {: RESULT = new AST_FUNC_DEC_STMT(t,funcName,null,sl);:}
                      ;

classDec        ::=   CLASS ID:name EXTENDS ID:parentName LBRACE cFieldList:l RBRACE       {: RESULT = new AST_CLASSDEC_CFIELD(name,parentName,l);:}
                      | CLASS ID:name LBRACE cFieldList:l RBRACE                                {: RESULT = new AST_CLASSDEC_CFIELD(name,null,l);:}
                      ;

exp			::=		INT:i							            	{: RESULT = new AST_EXP_INT(i);          :}
                    | MINUS INT:i                                   {: RESULT = new AST_EXP_INT(-i);         :}
                    | NIL       					            	{: RESULT = new AST_EXP_NIL();           :}
                    | STRING:str                                    {: RESULT = new AST_EXP_STRING(str);     :}
					| var:v							            	{: RESULT = new AST_EXP_VAR(v);          :}
					| exp:e1 BINOP:b  exp:e2			            	{: RESULT = new AST_EXP_BINOP(e1, b, e2);:}
					| var:v DOT ID:name LPAREN expList:body RPAREN  {: RESULT = new AST_EXPLIST_FUNC(name, v, body);:}
					| ID:name LPAREN expList:body RPAREN            {: RESULT = new AST_EXPLIST_FUNC(name, null, body);:}
					| var:v DOT ID:name LPAREN  RPAREN              {: RESULT = new AST_EXPLIST_FUNC(name, v, null);:}
					| ID:name LPAREN RPAREN                         {: RESULT = new AST_EXPLIST_FUNC(name, null, null);:}
					| LPAREN expParen:e RPAREN                           {: RESULT = new AST_EXP_PAREN(e);          :}
					;

var			::=		ID:name								{: RESULT = new AST_VAR_SIMPLE(name);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AST_VAR_FIELD(v,fieldName); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AST_VAR_SUBSCRIPT(v,e);     :}
					;

stmt		::=		varDec:vd                                                   {: RESULT = new AST_STMT_VARDEC(vd);         :}
                    | var:v ASSIGN exp:e SEMICOLON								{: RESULT = new AST_STMT_ASSIGN(v,e,null);      :}
                    | var:v ASSIGN newExp:ne SEMICOLON							{: RESULT = new AST_STMT_ASSIGN(v,null,ne);     :}
                    | RETURN exp:e SEMICOLON                                    {: RESULT = new AST_STMT_RETURN(e);        :}
                    | RETURN SEMICOLON                                          {: RESULT = new AST_STMT_RETURN(null);     :}
					| IF    LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_IF(cond,body); :}
					| WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_WHILE(cond,body); :}
					| var:v DOT ID:name LPAREN expList:body RPAREN SEMICOLON    {: RESULT = new AST_STMT_ID(name, v, body);:}
                    | ID:name LPAREN expList:body RPAREN SEMICOLON              {: RESULT = new AST_STMT_ID(name, null, body);:}
                    | var:v DOT ID:name LPAREN  RPAREN SEMICOLON                {: RESULT = new AST_STMT_ID(name, v, null);:}
                    | ID:name LPAREN RPAREN SEMICOLON                           {: RESULT = new AST_STMT_ID(name, null, null);:}
					;

newExp      ::=     NEW type:t                        {: RESULT = new AST_NEWEXP_NEW(t, null);:}
                    | NEW type:t LBRACK exp:e RBRACK  {: RESULT = new AST_NEWEXP_NEW(t, e);:}
                    ;

cField      ::=     varDec:vd                          {: RESULT = new AST_CFIELD_VARDEC(vd);:}
                    | funcDec:fd                       {: RESULT = new AST_CFIELD_FUNCDEC(fd);:}
                    ;

BINOP       ::=     PLUS     {: RESULT = new AST_BINOP(0);:}
                    | MINUS   {: RESULT = new AST_BINOP(1);:}
                    | TIMES   {: RESULT = new AST_BINOP(2);:}
                    | DIVIDE   {: RESULT = new AST_BINOP(3);:}
                    | LT   {: RESULT = new AST_BINOP(4);:}
                    | GT   {: RESULT = new AST_BINOP(5);:}
                    | EQ   {: RESULT = new AST_BINOP(6);:}
                    ;